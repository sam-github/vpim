From RFC 2425

5.8.2.  ABNF content-type definition

   The following ABNF uses the notation of RFC 2234, which also defines
   CRLF, WSP, DQUOTE, VCHAR, ALPHA, and DIGIT.  After the unfolding of
   any folded lines as described above, the syntax for a line of this
   content type is as follows:

   contentline  = [group "."] name *(";" param) ":" value CRLF
      ; When parsing a content line, folded lines MUST first
      ; be unfolded according to the unfolding procedure
      ; described above.
      ; When generating a content line, lines longer than 75
      ; characters SHOULD be folded according to the folding
      ; procedure described above.

   group        = 1*(ALPHA / DIGIT / "-")

   name         = x-name / iana-token

   iana-token   = 1*(ALPHA / DIGIT / "-")
      ; identifier registered with IANA

   x-name       = "x-" 1*(ALPHA / DIGIT / "-")
      ; Names that begin with "x-" or "X-" are
      ; reserved for experimental use, not intended for released
      ; products, or for use in bilateral agreements.

   param        = param-name "=" param-value *("," param-value)

   param-name   = x-name / iana-token

   param-value  = ptext / quoted-string

   ptext  = *SAFE-CHAR

   value = *VALUE-CHAR
         / valuespec      ; valuespec defined in section 5.8.4

   quoted-string = DQUOTE *QSAFE-CHAR DQUOTE

   NON-ASCII    = %x80-FF
      ; use restricted by charset parameter
      ; on outer MIME object (UTF-8 preferred)

   QSAFE-CHAR   = WSP / %x21 / %x23-7E / NON-ASCII
      ; Any character except CTLs, DQUOTE

   SAFE-CHAR    = WSP / %x21 / %x23-2B / %x2D-39 / %x3C-7E / NON-ASCII
      ; Any character except CTLs, DQUOTE, ";", ":", ","

   VALUE-CHAR   = WSP / VCHAR / NON-ASCII
      ; any textual character

5.8.3.  Pre-defined Parameters

   The following parameters and value types are defined for general use.

         predefined-param = encodingparm
                          / valuetypeparm
                          / languageparm
                          / contextparm

         encodingparm = "encoding" "=" encodingtype

         encodingtype = "b"       ; from RFC 2047
                    / iana-token  ; registered as described in
                                  ; section 15 of this document

         valuetypeparm = "value" "=" valuetype

         valuetype = "uri"        ; genericurl from secion 5 of RFC 1738
                    / "text"
                    / "date"
                    / "time"
                    / "date-time" ; date time
                    / "integer"
                    / "boolean"
                    / "float"
                    / x-name
                    / iana-token  ; registered as described in
                                  ; section 15 of this document

         languageparm = "language" "=" Language-Tag
             ; Language-Tag is defined in section 2 of RFC 1766

         contextparm = "context" "=" context

         context = x-name
                 / iana-token

5.8.4.  Pre-defined Value Types

   The format for values corresponding to the predefined valuetype
   specifications given above are defined.

   valuespec =  text-list
              / genericurl       ; from section 5 of RFC 1738
              / date-list
              / time-list
              / date-time-list
              / boolean
              / integer-list
              / float-list
              / iana-valuespec

   text-list = *TEXT-LIST-CHAR *("," *TEXT-LIST-CHAR)

   TEXT-LIST-CHAR = "\\" / "\," / "\n"
                  / <any VALUE-CHAR except , or \ or newline>
       ; Backslashes, newlines, and commas must be encoded.
       ; \n or \N can be used to encode a newline.

   date-list = date *("," date)

   time-list = time *("," time)

   date-time-list = date "T" time *("," date "T" time)

   boolean = "TRUE" / "FALSE"

   integer-list = integer *("," integer)

   integer = [sign] 1*DIGIT

   float-list = float *("," float)

   float = [sign] 1*DIGIT ["." 1*DIGIT]

   sign = "+" / "-"

   date = date-fullyear ["-"] date-month ["-"] date-mday

   date-fullyear = 4 DIGIT

   date-month = 2 DIGIT     ;01-12

   date-mday = 2 DIGIT      ;01-28, 01-29, 01-30, 01-31
                            ;based on month/year

   time = time-hour [":"] time-minute [":"] time-second [time-secfrac]
           [time-zone]

   time-hour = 2 DIGIT      ;00-23

   time-minute = 2 DIGIT    ;00-59

   time-second = 2 DIGIT    ;00-60 (leap second)

   time-secfrac = "," 1*DIGIT

   time-zone = "Z" / time-numzone

   time-numzome = sign time-hour [":"] time-minute

   iana-valuespec = <a publicly-defined valuetype format, registered
                     with IANA, as defined in section 15 of this
                     document>
